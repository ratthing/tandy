    1 REM TAMUSTAT BY RAY LOPEZ
   10 GOSUB "INIT"
   20 GOSUB "NEWDATA"
   30 GOTO "MENU"
  100 "INIT"
  110 CLEAR : DIM UD(150): DIM SD(150): DIM RE(24)
  120 RETURN 
  200 "NEWDATA"
  210 I$="": INPUT "NEW DATA (Y/N)? ";I$
  220 IF I$="Y" THEN GOTO 240
  230 GOTO 285
  240 INPUT "1 OR 2 VARS? ";RE(7): IF RE(7)>2 THEN GOTO 240: IF RE(7)<1 THEN GOTO 250
  260 INPUT "NUM OF OBS / VAR? ";RE(8): IF RE(8)>150/RE(7) THEN GOTO 260: IF RE(8)<2 THEN GOTO 260
  265 RE(9)=RE(7)*RE(8):CO=0
  270 FOR I=1 TO RE(7): FOR J=1 TO RE(8): CLS : WAIT 0: PRINT "VAR";I;" OBS";J;: INPUT ": ";V
  275 CO=CO+1:UD(CO)=V:SD(CO)=V: NEXT J: NEXT I:RE(10)=0:RE(20)=0: CLS 
  280 GOSUB "ZERO"
  285 RETURN 
  300 "ZERO"
  310 I=0:J=0:V=0:L1=0:L2=0:VX=0:SH=0:S1=0:S2=0:B=0:PN=0
  320 M1=0:M2=0:CK=0:T1=0:ME=0:CM=0:SE=0:IN=0:X1=0:Y1=0
  330 XX=0:YY=0:PX=0
  340 RETURN 
  400 "DESCRIP"
  402 L1=1:L2=RE(8):VX=1:SH=0
  404 WAIT 0: PRINT "COMPUTING..."
  406 FOR I=L1 TO L2:S1=S1+SD(I): NEXT I
  408 RE(1+SH)=S1/RE(8)
  410 FOR I=L1 TO L2:S2=S2+(SD(I)-RE(1+SH))^2: NEXT I
  412 RE(2+SH)=SQR (S2/(RE(8)-1)):RE(3+SH)=RE(2+SH)/SQR RE(8)
  414 RE(4+SH)=1.96*RE(3+SH):RE(10+SH)=RE(8)-1
  416 FOR J=1 TO RE(8)
  418 FOR I=L1 TO L2-1
  420 IF SD(I)>=SD(I+1) THEN GOTO 424
  422 B=SD(I):SD(I)=SD(I+1):SD(I+1)=B
  424 NEXT I
  426 NEXT J
  428 PN=L2/2-INT (L2/2)
  430 IF PN=0 THEN GOTO 436
  432 RE(5+SH)=SD((L2/2)+1)
  434 GOTO 438
  436 RE(5+SH)=SD(L1)+((SD(L2)-SD(L1))/2)
  438 RE(6+SH)=ABS (SD(L2)-SD(L1)):RE(10+SH)=1
  440 WAIT : PRINT "<ENTER> FOR VAR";VX
  442 PRINT "MEAN:";RE(1+SH): PRINT "STD DEV:";RE(2+SH)
  444 PRINT "SEM:";RE(3+SH): PRINT "MEDIAN:";RE(5+SH)
  446 PRINT "RANGE:";RE(6+SH)
  448 PRINT "95% CI+/-:";RE(4+SH)
  450 IF RE(7+SH)=2 THEN GOTO 454
  452 GOTO 458
  454 IF L2=RE(9) THEN GOTO 458
  456 L1=RE(8)+1:L2=RE(9):VX=2:SH=10:S1=0:S2=0: GOTO 404
  458 GOSUB "ZERO"
  460 RETURN 
  500 "TTEST1"
  505 GOSUB "ZERO"
  510 CK=RE(10)+RE(20)
  515 ON CK GOTO 545,525
  520 GOTO 599
  525 INPUT "VAR 1 OR 2?: ";IN: IF IN>2 THEN GOTO 525
  530 IF IN=1 THEN GOTO 545
  535 GOTO 555
  545 ME=RE(1):SE=RE(3): GOSUB 565
  550 GOTO 599
  555 ME=RE(11):SE=RE(13): GOSUB 565
  560 GOTO 599
  565 INPUT "COMP. MEAN?: ";CM
  570 T1=(ME-CM)/SE
  575 WAIT : PRINT "CALC T= ";T1
  580 PRINT "D.F.=";RE(8)-1
  585 RETURN 
  599 RETURN 
  600 "CORR"
  605 GOSUB "ZERO"
  610 IF RE(7)=2 THEN GOTO 620
  615 WAIT : PRINT "ERROR: ONLY 1 VAR": GOTO 699
  620 FOR I=1 TO RE(8)
  625 X1=X1+UD(I):Y1=Y1+UD(RE(8)+I):XX=XX+UD(I)^2:YY=YY+UD(RE(8)+I)^2:PX=PX+(UD(I)*UD(I+RE(8)))
  630 NEXT I
  635 R5=(RE(8)*PX)-(X1*Y1):R6=(RE(8)*XX)-X1^2:R7=(RE(8)*YY)-Y1^2
  640 RC=R5/SQR (R6*R7)
  645 PRINT "r^2=";RC: PRINT "D.F.=";RE(8)-2
  699 RETURN 
  700 "MENU"
  705 I$=""
  710 WAIT 0: PRINT "E,D,T,C,Q,? > ":I$=INKEY$ 
  715 IF I$="E" THEN GOSUB "NEWDATA": GOTO 705
  720 IF I$="D" THEN GOSUB "DESCRIP": GOTO 705
  725 IF I$="T" THEN GOSUB "TTEST1": GOTO 705
  730 IF I$="C" THEN GOSUB "CORR": GOTO 705
  735 IF I$="Q" THEN GOTO "BYE"
  740 IF I$="?" THEN GOSUB "HELP"
  745 GOTO 705
  800 "HELP"
  810 PRINT "E=ENTER DATA"
  815 PRINT "D=DESCRIPTIVE STATS"
  820 PRINT "T=1-SAMPLE TTEST"
  825 PRINT "C=PEARSON CORR"
  830 PRINT "Q=QUIT"
  835 PRINT "?=HELP"
  840 RETURN 
 9000 "BYE"
 9999 END
